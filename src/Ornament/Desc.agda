{-# OPTIONS --type-in-type --with-K #-}


module Ornament.Desc where

open Agda.Primitive renaming (Set to Type)

open import Data.Unit
open import Data.Empty
open import Data.Product
open import Data.Sum hiding (mapâ‚‚)
open import Data.Nat
open import Function.Base

open import Relation.Binary.PropositionalEquality using (_â‰¡_; cong; sym; refl; subst) renaming (trans to _âˆ™_; substâ‚‚ to subst2)


private variable
  J K L : Type
  A B C X Y Z : Type
  P Pâ€² : Type


infixr 10 _âˆ·_
infixr 10 _â–·_

_â‡‰_ : (X Y : A â†’ Type) â†’ Type
X â‡‰ Y = âˆ€ a â†’ X a â†’ Y a

_â‡¶_ : (X Y : A â†’ B â†’ Type) â†’ Type
X â‡¶ Y = âˆ€ a b â†’ X a b â†’ Y a b

liftM2 : (A â†’ B â†’ C) â†’ (X â†’ A) â†’ (X â†’ B) â†’ X â†’ C
liftM2 f g h x = f (g x) (h x)

! : {A : Type} â†’ A â†’ âŠ¤
! _ = tt

--* telescopes
data Tel (P : Type) : Type
âŸ¦_âŸ§tel : (Î“ : Tel P) â†’ P â†’ Type

-- Î“ âŠ¢ A reads as "a value of A in the context of Î“"
_âŠ¢_ : (Î“ : Tel P) â†’ Type â†’ Type
Î“ âŠ¢ A = Î£ _ âŸ¦ Î“ âŸ§tel â†’ A

-- V âŠ§ S reads as "an interpretation of S"
_âŠ§_ : (V : Tel P) â†’ V âŠ¢ Type â†’ Type
V âŠ§ S = âˆ€ p â†’ S p

data Tel P where
  âˆ…   : Tel P
  _â–·_ : (Î“ : Tel P) (S : Î“ âŠ¢ Type) â†’ Tel P

âŸ¦ âˆ…     âŸ§tel p = âŠ¤
âŸ¦ Î“ â–· S âŸ§tel p = Î£ (âŸ¦ Î“ âŸ§tel p) (S âˆ˜ (p ,_)) 

-- ExTel Î“ reads as "extension of Î“", and represents a sequence of dependent types which can act as if they were right after the last element of Î“
ExTel : Tel âŠ¤ â†’ Type
ExTel Î“ = Tel (âŸ¦ Î“ âŸ§tel tt)

-- the same as _âŠ¢_, but shortens {V : ExTel Î“} â†’ V âŠ¢ A to Î“ & V âŠ¢ A
_&_âŠ¢_ : (Î“ : Tel âŠ¤) â†’ ExTel Î“ â†’ Type â†’ Type
Î“ & V âŠ¢ A = V âŠ¢ A

âŸ¦_&_âŸ§tel : (Î“ : Tel âŠ¤) (V : ExTel Î“) â†’ Type
âŸ¦ Î“ & V âŸ§tel = Î£ (âŸ¦ Î“ âŸ§tel tt) âŸ¦ V âŸ§tel

private variable
  Î“ Î” Î˜ : Tel P
  U V W : ExTel Î“

-- parameter transformation
Cxf : (Î“ Î” : Tel âŠ¤) â†’ Type
Cxf Î“ Î” = âŸ¦ Î“ âŸ§tel tt â†’ âŸ¦ Î” âŸ§tel tt

-- parameter-variable transformation
Exf : (Î“ Î” : Tel âŠ¤) (V : ExTel Î“) (W : ExTel Î”) â†’ Type
Exf Î“ Î” V W = âŸ¦ Î“ & V âŸ§tel â†’ âŸ¦ Î” & W âŸ§tel

-- variable transformation
Vxf : (Î“ : Tel âŠ¤) (V W : ExTel Î“) â†’ Type
Vxf Î“ V W = âˆ€ {p} â†’ âŸ¦ V âŸ§tel p â†’ âŸ¦ W âŸ§tel p

-- variable transformation over parameter transformation
VxfO : (f : Cxf Î“ Î”) (V : ExTel Î“) (W : ExTel Î”) â†’ Type
VxfO f V W = âˆ€ {p} â†’ âŸ¦ V âŸ§tel p â†’ âŸ¦ W âŸ§tel (f p)

-- combinators
over : {f : Cxf Î“ Î”} â†’ VxfO f V W â†’ Exf Î“ Î” V W
over g (p , v) = _ , g v

Cxf-Exf : Cxf Î“ Î” â†’ Exf Î“ Î” âˆ… âˆ…
Cxf-Exf f (p , _) = f p , _ 

Vxf-Exf : Vxf Î“ V W â†’ Exf Î“ Î“ V W
Vxf-Exf f (p , v) = p , f v

Vxf-â–· : (f : Vxf Î“ V W) (S : W âŠ¢ Type) â†’ Vxf Î“ (V â–· (S âˆ˜ Vxf-Exf f)) (W â–· S)
Vxf-â–· f S (p , v) = f p , v

VxfO-â–· : âˆ€ {c : Cxf Î“ Î”} (f : VxfO c V W) (S : W âŠ¢ Type) â†’ VxfO c (V â–· (S âˆ˜ over f)) (W â–· S)
VxfO-â–· f S (p , v) = f p , v

VxfO-â–·-map : {c : Cxf Î“ Î”} (f : VxfO c V W) (S : W âŠ¢ Type) (T : V âŠ¢ Type) â†’ (âˆ€ p v â†’ T (p , v) â†’ S (c p , f v)) â†’ VxfO c (V â–· T) (W â–· S)
VxfO-â–·-map f S T m (v , t) = (f v , m _ v t)

Exf-â–· : (f : Exf Î“ Î” V W) (S : W âŠ¢ Type) â†’ Exf Î“ Î” (V â–· (S âˆ˜ f)) (W â–· S)
Exf-â–· f S (p , v , s) = let (p' , v') = f (p , v) in p' , v' , s

&-â–· : âˆ€ {S} â†’ (p : âŸ¦ Î” & W âŸ§tel) â†’ S p â†’ âŸ¦ Î” & W â–· S âŸ§tel
&-â–· (p , v) s = p , v , s

&-drop-â–· : âˆ€ {S} â†’ âŸ¦ Î“ & V â–· S âŸ§tel â†’ âŸ¦ Î“ & V âŸ§tel
&-drop-â–· (p , v , s) = p , v

âŠ§-â–· : âˆ€ {V : ExTel Î“} {S} â†’ V âŠ§ S â†’ Vxf Î“ V (V â–· S)
âŠ§-â–· s v = v , s (_ , v)


--* descriptions
-- information bundles (see ConI), a bundle If effectively requests an extra piece of information of, e.g., type ğŸ™i when defining a constructor using ğŸ™
record Info : Type where
  field
    ğŸ™i : Type
    Ïi : Type
    Ïƒi : (S : Î“ & V âŠ¢ Type) â†’ Type
    Î´i : Tel âŠ¤ â†’ Type â†’ Type
    -- informed descriptions know who they are! we don't need to introduce ourselves twice, unlike newcomers like (S : Î“ & V âŠ¢ Type)

open Info public

-- information transformers, if there is a transformation InfoF Ifâ€² If from the more specific bundle Ifâ€² to the less specific bundle If, then a DescI Ifâ€² can act as a DescI If
record InfoF (L R : Info) : Type where
  field
    ğŸ™f : L .ğŸ™i â†’ R .ğŸ™i
    Ïf : L .Ïi â†’ R .Ïi
    Ïƒf : {V : ExTel Î“} (S : V âŠ¢ Type) â†’ L .Ïƒi S â†’ R .Ïƒi S
    Î´f : âˆ€ Î“ A â†’ L .Î´i Î“ A â†’ R .Î´i Î“ A

open InfoF public

id-InfoF : âˆ€ {X} â†’ InfoF X X
id-InfoF .ğŸ™f = id
id-InfoF .Ïf = id
id-InfoF .Ïƒf _ = id
id-InfoF .Î´f _ _ = id

_âˆ˜InfoF_ : âˆ€ {X Y Z} â†’ InfoF Y Z â†’ InfoF X Y â†’ InfoF X Z
(Ï• âˆ˜InfoF Ïˆ) .ğŸ™f x = Ï• .ğŸ™f (Ïˆ .ğŸ™f x)
(Ï• âˆ˜InfoF Ïˆ) .Ïf x = Ï• .Ïf (Ïˆ .Ïf x)
(Ï• âˆ˜InfoF Ïˆ) .Ïƒf S x = Ï• .Ïƒf S (Ïˆ .Ïƒf S x)
(Ï• âˆ˜InfoF Ïˆ) .Î´f Î“ A x = Ï• .Î´f Î“ A (Ïˆ .Î´f Î“ A x)

-- no extra information at all! the magic of eta-expansion makes sure that a DescI Plain never gets into someone's way
Plain : Info
Plain .ğŸ™i = âŠ¤
Plain .Ïi = âŠ¤
Plain .Ïƒi _ = âŠ¤
Plain .Î´i _ _ = âŠ¤

private variable
  If Ifâ€² : Info

-- a DescI If Î“ J describes a PIType Î“ J, augmented by the bundle If, note that If has no effect the fixpoint!
data DescI (If : Info) (Î“ : Tel âŠ¤) (J : Type) : Type
data Î¼ (D : DescI If Î“ J) (p : âŸ¦ Î“ âŸ§tel tt) : J â†’ Type
data ConI (If : Info) (Î“ : Tel âŠ¤) (J : Type) (V : ExTel Î“) : Type where
  -- ... â†’ X p (j (p , v)) 
  ğŸ™ : {if : If .ğŸ™i} (j : Î“ & V âŠ¢ J) â†’ ConI If Î“ J V

  -- ... â†’ X (g p) (j (p , v)) â†’ ...
  Ï : {if : If .Ïi} (j : Î“ & V âŠ¢ J) (g : Cxf Î“ Î“) (C : ConI If Î“ J V) â†’ ConI If Î“ J V
  -- maybe g could be Î“ & V âŠ¢ âŸ¦ Î“ âŸ§tel tt

  -- ... â†’ (s : S (p , v)) â†’ let w = h (v , s) in ...
  Ïƒ : (S : V âŠ¢ Type) {if : If .Ïƒi S} (h : Vxf Î“ (V â–· S) W) (C : ConI If Î“ J W) â†’ ConI If Î“ J V

  -- ... â†’ (r : Î¼ R (g (p , v)) (j (p , v))) â†’ let w = h (v , r) in ...
  Î´ : {if : If .Î´i Î” K} {iff : InfoF Ifâ€² If}
      (j : Î“ & V âŠ¢ K) (g : Î“ & V âŠ¢ âŸ¦ Î” âŸ§tel tt) (R : DescI Ifâ€² Î” K) (h : Vxf Î“ (V â–· liftM2 (Î¼ R) g j) W)
      (C : ConI If Î“ J W)
    â†’ ConI If Î“ J V


-- the variable transformations (Vxf) in Ïƒ and Î´ let us choose which variables we retain for the remainder of the description
-- using them, we define "smart" Ïƒ and Î´, where the + variant retains the last variable, while the - variant drops it
Ïƒ+ : (S : Î“ & V âŠ¢ Type) â†’ {if : If .Ïƒi S} â†’ ConI If Î“ J (V â–· S) â†’ ConI If Î“ J V
Ïƒ+ S {if = if} C = Ïƒ S {if = if} id C

Ïƒ- : (S : Î“ & V âŠ¢ Type) â†’ {if : If .Ïƒi S} â†’ ConI If Î“ J V â†’ ConI If Î“ J V
Ïƒ- S {if = if} C = Ïƒ S {if = if} projâ‚ C

Î´+ : {if : If .Î´i Î” K} {iff : InfoF Ifâ€² If} â†’ (j : Î“ & V âŠ¢ K) (g : Î“ & V âŠ¢ âŸ¦ Î” âŸ§tel tt) (D : DescI Ifâ€² Î” K) â†’ ConI If Î“ J (V â–· liftM2 (Î¼ D) g j) â†’ ConI If Î“ J V
Î´+ {if = if} {iff = iff} j g R D = Î´ {if = if} {iff = iff} j g R id D

Î´- : {if : If .Î´i Î” K} {iff : InfoF Ifâ€² If} â†’ (j : Î“ & V âŠ¢ K) (g : Î“ & V âŠ¢ âŸ¦ Î” âŸ§tel tt) (D : DescI Ifâ€² Î” K) â†’ ConI If Î“ J V â†’ ConI If Î“ J V
Î´- {if = if} {iff = iff} j g R D = Î´ {if = if} {iff = iff} j g R projâ‚ D

-- ordinary recursive field
Ï0 : {if : If .Ïi} {V : ExTel Î“} â†’ V âŠ¢ J â†’ ConI If Î“ J V â†’ ConI If Î“ J V
Ï0 {if = if} r D = Ï {if = if} r id D


data DescI If Î“ J where
  []  : DescI If Î“ J
  _âˆ·_ : ConI If Î“ J âˆ… â†’ DescI If Î“ J â†’ DescI If Î“ J 

Con  = ConI Plain
Desc = DescI Plain

data Tag Î“ : Type where
  CT : ExTel Î“ â†’ Tag Î“
  DT : Tag Î“

-- PIType Î“ J reads as "type with parameters Î“ and index J", the universe of types we will take the fixpoint over
PIType : Tel âŠ¤ â†’ Type â†’ Type
PIType Î“ J = âŸ¦ Î“ âŸ§tel tt â†’ J â†’ Type

module _ {If : Info} where
  UnTag : (Î“ : Tel âŠ¤) (J : Type) â†’ Tag Î“ â†’ Type
  UnTag Î“ J (CT V) = ConI If Î“ J V
  UnTag Î“ J DT     = DescI If Î“ J

  UnFun : (Î“ : Tel âŠ¤) (J : Type) â†’ Tag Î“ â†’ Type
  UnFun Î“ J (CT V) = âŸ¦ Î“ & V âŸ§tel â†’ J â†’ Type
  UnFun Î“ J DT     = PIType Î“ J

  -- interpretation
  âŸ¦_âŸ§ : {t : Tag Î“} â†’ UnTag Î“ J t â†’ PIType Î“ J â†’ UnFun Î“ J t
  âŸ¦_âŸ§ {t = CT V} (ğŸ™ j)         X pv i         = i â‰¡ j pv
  âŸ¦_âŸ§ {t = CT V} (Ï j f D)     X pv@(p , v) i = X (f p) (j pv) Ã— âŸ¦ D âŸ§ X pv i
  âŸ¦_âŸ§ {t = CT V} (Ïƒ S h D)     X pv@(p , v) i = Î£[ s âˆˆ S pv ] âŸ¦ D âŸ§ X (p , h (v , s)) i
  âŸ¦_âŸ§ {t = CT V} (Î´ j g R h D) X pv@(p , v) i = Î£[ s âˆˆ Î¼ R (g pv) (j pv) ] âŸ¦ D âŸ§ X (p , h (v , s)) i
  âŸ¦_âŸ§ {t = DT}   []            X p i = âŠ¥
  âŸ¦_âŸ§ {t = DT}   (C âˆ· D)       X p i = (âŸ¦ C âŸ§ X (p , tt) i) âŠ (âŸ¦ D âŸ§ X p i) 


data Î¼ D p where
  con : âˆ€ {i} â†’ âŸ¦ D âŸ§ (Î¼ D) p i â†’ Î¼ D p i


fold : âˆ€ {D : DescI If Î“ J} {X}
     â†’ âŸ¦ D âŸ§ X â‡¶ X â†’ Î¼ D â‡¶ X
     
mapDesc : âˆ€ {D' : DescI If Î“ J} (D : DescI If Î“ J) {X}
        â†’ âˆ€ p j  â†’ âŸ¦ D' âŸ§ X â‡¶ X â†’ âŸ¦ D âŸ§ (Î¼ D') p j â†’ âŸ¦ D âŸ§ X p j
        
mapCon : âˆ€ {D' : DescI If Î“ J} {V} (C : ConI If Î“ J V) {X}
       â†’ âˆ€ p j v â†’ âŸ¦ D' âŸ§ X â‡¶ X â†’ âŸ¦ C âŸ§ (Î¼ D') (p , v) j â†’ âŸ¦ C âŸ§ X (p , v) j

fold f p i (con x) = f p i (mapDesc _ p i f x)

mapDesc (C âˆ· D) p j f (injâ‚ x) = injâ‚ (mapCon C p j tt f x)
mapDesc (C âˆ· D) p j f (injâ‚‚ y) = injâ‚‚ (mapDesc D p j f y)

mapCon (ğŸ™ k)         p j v f      x  = x
mapCon (Ï k g C)     p j v f (r , x) = fold f (g p) (k (p , v)) r , mapCon C p j v f x
mapCon (Ïƒ S h C)     p j v f (s , x) = s , mapCon C p j (h (v , s)) f x
mapCon (Î´ k g R h C) p j v f (r , x) = r , mapCon C p j (h (v , r)) f x

par : Î“ âŠ¢ A â†’ Î“ & V âŠ¢ A 
par f = f âˆ˜ (tt ,_) âˆ˜ projâ‚

-- I hope this does what I think it does
top : âˆ€ {S} â†’ (Î“ â–· S) âŠ§ Î» { (t , p , _) â†’ S (t , p) }
top = projâ‚‚ âˆ˜ projâ‚‚

pop : âˆ€ {S} â†’ Î“ âŠ¢ A â†’ (Î“ â–· S) âŠ¢ A
pop f (t , p , s) = f (t , p)

-- examples
module Descriptions where
  NatD : Desc âˆ… âŠ¤
  NatD = ğŸ™ _
       âˆ· Ï0 _ (ğŸ™ _)
       âˆ· []

  VecD : Desc (âˆ… â–· const Type) â„•
  VecD = ğŸ™ (const 0)
       âˆ· Ïƒ- (par top) (Ïƒ+ (const â„•) (Ï0 top (ğŸ™ (suc âˆ˜ top))))
       âˆ· []

  Vec = Î¼ VecD

  vec-1 : Vec (tt , âŠ¤) 1
  vec-1 = con (injâ‚‚ (injâ‚ (tt , 0 , ((con (injâ‚ refl)) , refl))))

  DigitD : Desc (âˆ… â–· const Type) âŠ¤
  DigitD = Ïƒ- (par top) (ğŸ™ _)
         âˆ· Ïƒ- (par top) (Ïƒ- (par top) (ğŸ™ _))
         âˆ· Ïƒ- (par top) (Ïƒ- (par top) (Ïƒ- (par top) (ğŸ™ _)))
         âˆ· []
         
  data Node (A : Type) : Type where
    two   : A â†’ A     â†’ Node A
    three : A â†’ A â†’ A â†’ Node A

  FingerD : Desc (âˆ… â–· const Type) âŠ¤
  FingerD = ğŸ™ _
          âˆ· Ïƒ- (par top) (ğŸ™ _)
          âˆ· Î´- _ (par ((tt ,_) âˆ˜ top)) DigitD (Ï _ (Î» { (_ , A) â†’ (_ , Node A) }) (Î´- _ (par ((tt ,_) âˆ˜ top)) DigitD (ğŸ™ _)))
          âˆ· []

